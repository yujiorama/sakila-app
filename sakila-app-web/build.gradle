dependencies {
    implementation(project(':sakila-app-domain'))
    implementation('org.springframework.boot:spring-boot-starter-web')
    implementation('com.fasterxml.jackson.module:jackson-module-kotlin')
}

sourceSets {
    integrationTest {
        compileClasspath += main.output + test.output
        runtimeClasspath += main.output + test.output

        java {
            srcDir file('src/test/java')
        }

        kotlin {
            srcDir file('src/test/kotlin')
        }

        resources {
            srcDir file('src/test/resources')
        }
    }
}

configurations {
    integrationTestImplementation {
        extendsFrom testImplementation
    }

    integrationTestRuntime {
        extendsFrom testRuntime
    }
}

task integrationTest(type: Test) {
    group = 'verification'

    testClassesDirs = sourceSets.integrationTest.output.classesDirs
    classpath = sourceSets.integrationTest.runtimeClasspath

    filter {
        includeTestsMatching '*IntegrationTest'
    }

    useJUnitPlatform()
    systemProperty 'spring.profiles.active', 'it'
    environment = System.getenv() + [
        'DOCKER_HOST'      : env.fetch('DOCKER_HOST', 'unix:///var/run/docker.sock'),
        'DOCKER_TLS_VERIFY': env.fetch('DOCKER_TLS_VERIFY', ''),
        'DOCKER_CERT_PATH' : env.fetch('DOCKER_CERT_PATH', ''),
    ]
    testLogging {
        events 'failed',
            'passed',
            'skipped',
            'standard_error',
            'standard_out'
        exceptionFormat 'full'
        showCauses true
        showExceptions true
        showStackTraces true
    }
}

check.dependsOn integrationTest

test.filter {
    excludeTestsMatching '*IntegrationTest'
}
